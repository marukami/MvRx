apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "jacoco"
apply plugin: "com.vanniktech.maven.publish"

android {
    compileSdkVersion 28
    resourcePrefix 'mvrx_'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildToolsVersion "$buildToolsVersion"
    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

androidExtensions {
    experimental = true
}

jacoco {
    toolVersion = "0.8.2"
}

dependencies {
    implementation project(":mvrx")
    implementation "androidx.navigation:navigation-fragment-ktx:2.2.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation 'junit:junit:4.13'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

// Ensure custom source sets are included:
// https://www.virag.si/2015/01/publishing-gradle-android-library-to-jcenter/
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

tasks.withType(Javadoc).all {
    enabled = false
}

task coverage(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
    ]
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: excludes)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: excludes)
    classDirectories = files([debugTree], [kotlinDebugTree])

    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs,
            "${project.projectDir}/src/main/kotlin"
    ])
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
}
